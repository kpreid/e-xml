pragma.syntax("0.9")

# --- Just imports ---

def makeDocumentBuilderFactory := <import:javax.xml.parsers.makeDocumentBuilderFactory>
def makeDOMSource := <import:javax.xml.transform.dom.makeDOMSource>
def makeFlexList := <elib:tables.makeFlexList>
def makeInputSource := <import:org.xml.sax.makeInputSource>
def makeNode := <import:org.w3c.dom.makeNode>
def makeStreamResult := <import:javax.xml.transform.stream.makeStreamResult>
def makeStringReader := <import:java.io.makeStringReader>
def OutputKeys := <import:javax.xml.transform.makeOutputKeys>
def OneArgFunc := <type:org.erights.e.elib.util.OneArgFunc>
def XPathConstants := <import:javax.xml.xpath.makeXPathConstants>

def ATTRIBUTE_NODE := makeNode.getATTRIBUTE_NODE()
def ATTRIBUTE_SET_NODE {} # our own invention
def COMMENT_NODE := makeNode.getCOMMENT_NODE()
def DOCUMENT_FRAGMENT_NODE := makeNode.getDOCUMENT_FRAGMENT_NODE()
def ELEMENT_NODE := makeNode.getELEMENT_NODE()
def TEXT_NODE := makeNode.getTEXT_NODE()

# --- Stateful objects which are reused ---

def makeDocumentBuilder := makeDocumentBuilderFactory.newInstance()
makeDocumentBuilder.setNamespaceAware(true)
def documentBuilder := makeDocumentBuilder.newDocumentBuilder()

def fragmentOutputTransformer := <import:javax.xml.transform.makeTransformerFactory>.newInstance().newTransformer()
fragmentOutputTransformer.setOutputProperty(OutputKeys.getOMIT_XML_DECLARATION(), "yes");

def xpath := <import:javax.xml.xpath.makeXPathFactory>.newInstance().newXPath()

# --- Code ---

# Temporary kludge. We use this namespace to construct a document around the provided document fragment. This should be replaced with arranging to enter the parser at the proper point
def docHackElement := "THISDOESNOTEXISTINTHEDOCUMENTHOPEFULLY"

def makeFragmentWrapper

def makeNodeListWrapper(nodeList) {
    def list := makeFlexList.make()
    for i in 0..!(nodeList.getLength()) {
        list.push(makeFragmentWrapper(nodeList.item(i)))
    }
    return list.snapshot()
}

def makeAttributeSet(attrList :List) {
    # This implements the same API as DocumentFragment insofar as we need it internally
    def childNodeList {
        to getLength() { return attrList.size() }
        to item(i) { return attrList[i] }
    }
    def attributeSet {
        to getOwnerDocument() { return attrList[0].getOwnerDocument() }
        to getNodeType() { return ATTRIBUTE_SET_NODE }
        to asEList() { return attrList }
        to getChildNodes() { return childNodeList }
    }
    return attributeSet
}

bind makeFragmentWrapper(frag0) {
    # All lone nodes become wrapped in document fragments
    def frag := if (frag0.getNodeType() == ATTRIBUTE_NODE) {
        makeAttributeSet([frag0])
    } else if (frag0.getNodeType() == DOCUMENT_FRAGMENT_NODE) {
        frag0
    } else {
        def wrapper := frag0.getOwnerDocument().createDocumentFragment()
        wrapper.appendChild(frag0.cloneNode(true)) # XXX O(n) -- I tried to squeeze this out and it popped up elsewhere. TODO: Switch to our own tree implementation with a DOM-interface-adapter to provide to XPath.
        wrapper
    }
    
    def xmlTree {
        /** XXX The output of this is not always a valid E expression because it does not respect $/@/` escaping yet. */
        to __printOn(out) {
            # XXX $-escaping
            # XXX print/quote distinction (not exposed in EoJ)
            if (frag.getNodeType() == ATTRIBUTE_SET_NODE) {
                def [tw, sb] := <elib:oldeio.makeTextWriter>.makeBufferingPair()
                def wrapper := frag.getOwnerDocument().createElement("a")
                for attr in frag.asEList() {
                    wrapper.setAttributeNode(attr.cloneNode(true))
                }
                fragmentOutputTransformer.transform(makeDOMSource(wrapper), makeStreamResult(tw))
                def `<a @string/>` := sb.snapshot()
                out.print("xmlattr`", string, "`")
            } else {
                out.print("xml`")
                fragmentOutputTransformer.transform(makeDOMSource(frag), makeStreamResult(out))
                out.print("`")
            }
        }
        
        /** Text with no elements will coerce to a string. Attributes will coerce to their (string) values. */
        to __conformTo(guard) {
            # XXX ought to early exit if the guard wouldn't accept any string
            def children := frag.getChildNodes()
            def [tw, sb] := <elib:oldeio.makeTextWriter>.makeBufferingPair()
            var kind := null
            for i in 0..!(children.getLength()) {
                def child := children.item(i)
                if (child.getNodeType() == TEXT_NODE && (kind == null || kind == TEXT_NODE)) {
                    kind := TEXT_NODE
                    tw.print(child.getData())
                } else if (child.getNodeType() == ATTRIBUTE_NODE && kind == null) {
                    # won't stringify more than one attribute at once, concatenation doesn't make sense
                    kind := ATTRIBUTE_NODE
                    tw.print(child.getValue())
                } else if (child.getNodeType() == COMMENT_NODE && kind == null) {
                    # ignore comments entirely
                } else {
                    # We encountered incompatible multiple children, or an unrecognized child, so fail to coerce to a string
                    return xmlTree
                }
            }
            # If guard accepts our string, provide it
            if (sb.snapshot() =~ _ :guard) {
                return sb.snapshot()
            } else {
                return xmlTree
            }
        }
        
        /** Given an XPath expression object, return a collection whose values are the XML nodes it matches. */
        to get(xpathObj) {
            # XXX this is wrong with regard to namespaces; namespaces should be carried with the xpath expr
            def xpathExpr := xpathObj.getXPathExpression()
            return makeNodeListWrapper(xpath."evaluate(String, Object, QName)"(xpathExpr, frag, XPathConstants.getNODESET()))
            
        }
    }
    
    return xmlTree
    
}

def makeXMLTree {
    /** parseErrorHandler should be (equivalent to) an ejector, unless it is desired to collect and report on multiple parse errors. */
    to fromFragmentString(xmlString :String, parseErrorHandler :OneArgFunc, warningHandler :OneArgFunc) {
        require(xmlString.indexOf(docHackElement) == -1)
        def documentString := `<$docHackElement>$xmlString</$docHackElement>`
        
        documentBuilder.reset()
        
        # We want to handle errors per our parameters, but this is necessary anyway, as per experience, the default error handler is capability-violating, printing error messages to stderr/stdout. See notes in DocumentBuilder.safej.
        documentBuilder.setErrorHandler(def myErrorHandler {
            to error(saxException) {
                # Non-fatal errors may continue parsing. The handler should return rather than exiting in order to indicate this.
                parseErrorHandler(saxException)
            }
            to fatalError(saxException) {
                parseErrorHandler(saxException)
                throw(saxException) # doesn't reach here if the handler is an ejector
            }
            to warning(saxException) {
                warningHandler(saxException)
            }
        })
        
        def document := documentBuilder.parse(makeInputSource(makeStringReader(documentString)))
        def frag := document.createDocumentFragment()
        def hackElement := document.getDocumentElement()
        while (hackElement.getFirstChild() =~ c :notNull) {
            frag.appendChild(c)
        }
        return makeFragmentWrapper(frag)
    }
}
